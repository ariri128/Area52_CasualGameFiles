/*
 * Draws an outline around a furniture mesh
*/

Shader "Alien Apartments/Outline"
{
    Properties
    {
        _OutlineColor ("Outline Color", Color) = (1, 0.85, 0.25, 1)
        _OutlineWidth ("Outline Width (meters)", Range(0, 0.1)) = 0.015
        [ToggleUI] _Flat ("Flat (rugs)", Float) = 0
        [Enum(UnityEngine.Rendering.CullMode)] _Cull ("Cull", Float) = 1
        _DepthOffset ("Depth Offset", Float) = 0
        [HideInInspector] _CenterOS ("Center (set by script)", Vector) = (0, 0, 0, 0)
    }

    SubShader
    {
        Tags { "RenderType" = "Opaque" "RenderPipeline" = "UniversalPipeline" "Queue" = "Geometry" }

        Pass
        {
            Name "Outline"
            Tags { "LightMode" = "SRPDefaultUnlit" }

            Cull [_Cull]
            Offset 0, [_DepthOffset]
            ZWrite On

            HLSLPROGRAM
            #pragma vertex vert
            #pragma fragment frag

            #include "Packages/com.unity.render-pipelines.universal/ShaderLibrary/Core.hlsl"

            CBUFFER_START(UnityPerMaterial)
                half4 _OutlineColor;
                float _OutlineWidth;
                float _Flat;
                float _Cull;
                float _DepthOffset;
                float4 _CenterOS;
            CBUFFER_END

            struct Attributes
            {
                float4 positionOS : POSITION;
                float3 normalOS   : NORMAL;
            };

            struct Varyings
            {
                float4 positionCS : SV_POSITION;
            };

            Varyings vert (Attributes input)
            {
                Varyings output;

                float3 positionWS = TransformObjectToWorld(input.positionOS.xyz);
                float3 direction;

                if (_Flat > 0.5)
                {
                    float3 centerWS = TransformObjectToWorld(_CenterOS.xyz);
                    direction = positionWS - centerWS;
                    direction.y = 0;
                    direction /= max(length(direction), 0.00001);
                }
                else
                {
                    direction = normalize(TransformObjectToWorldNormal(input.normalOS));
                }

                positionWS += direction * _OutlineWidth;
                output.positionCS = TransformWorldToHClip(positionWS);
                return output;
            }

            half4 frag (Varyings input) : SV_Target
            {
                return _OutlineColor;
            }
            ENDHLSL
        }
    }
}